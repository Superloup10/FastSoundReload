buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}

project.ext.buildnumber = 0
version = mod_version

apply plugin: "kotlin"
apply plugin: "idea"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'
apply plugin: "com.github.johnrengelman.shadow"

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

if (System.getenv().BUILD_NUMBER) {
    project.buildnumber = System.getenv().BUILD_NUMBER
    version += "." + System.getenv().BUILD_NUMBER
} else {
    logger.lifecycle "SETTING BUILD_NUMBER TO 0"
}

minecraft {
    version = mc_version + "-" + forge_version
    runDir = "run"
    mappings = project.mcp_mappings
    replace "@VERSION@", project.version
    replace "@MC_VERSION", project.minecraft.version
}

idea { module { inheritOutputDirs = true } }

repositories {
    maven {
        name = "kotlin"
        url = "http://maven.shadowfacts.net/"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile "net.shadowfacts:Forgelin:1.7.3"
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile "org.jetbrains.kotlin:kotlin-runtime"
    implementation('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.fsr.refmap.json"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.fsr.json',
            "FMLCorePlugin": "fr.wolfdev.asm.FSRLoadingPlugin",
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true'
    )
}

shadowJar {
    dependsOn reobfJar
    classifier ""
    exclude "LICENSE.txt", "dummyThing"
    dependencies {
        include dependency('org.spongepowered:mixin')
    }
}

artifacts {
    archives shadowJar
}